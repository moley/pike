plugins {
    id "com.jfrog.bintray" version "1.8.3"
}

subprojects {

    def mavenUrl = System.getProperty("maven.url")
    def mavenUser = System.getProperty("maven.user")
    def mavenPwd = System.getProperty("maven.password")

    apply plugin: 'groovy'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    dependencies {
        compile localGroovy()
        compile gradleApi()
    }

    repositories {
        mavenCentral()
        maven {
            url "http://mvnrepository.com/artifact/"
        }
        jcenter()
    }

    tasks.create('sourceJar', Jar) {
        from sourceSets.main.allGroovy
    }

    group = 'org.pike'
    version = project.rootProject.file('releasenotes.md').text.split("\n").first().replaceAll("\\*", "").trim()

    test {
        testLogging {
            events "started", "skipped", "failed"
            exceptionFormat 'full'
            showStackTraces = 'true'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    project.plugins.withId('maven-publish') {
        publishing {
            repositories {
                maven {
                    credentials {
                        username mavenUser
                        password mavenPwd
                    }
                    url mavenUrl
                }
            }
            publications {
                maven(MavenPublication) {
                    from components.java
                    artifact project.sourceJar {
                        classifier 'sources'
                    }
                }
            }
        }
    }

    project.plugins.withId('com.jfrog.bintray') {
        println "Name: " + project.name
        bintray {
            user = System.properties['bintray.username']
            key = System.properties['bintray.password']

            println user + "-" + key

            publications = ['maven']
            pkg {
                repo = 'pike'
                name = "${project.name}"
                desc = 'collection of gradle tasks to do administrational things'
                websiteUrl = 'https://github.com/moley/pike'
                issueTrackerUrl = 'https://github.com/moley/pike'
                vcsUrl = 'https://github.com/moley/pike.git'
                licenses = ['Apache-2.0']
                labels = ['pike', 'gradle', 'gorilla']
                attributes = ['plat': ['win', 'linux', 'osx']]
                publicDownloadNumbers = true
                version.attributes = ['gradle-plugin': "org.pike.${name}:org.pike:${name}"]
            }
        }
    }

    tasks.build.dependsOn tasks.jacocoTestReport

}






